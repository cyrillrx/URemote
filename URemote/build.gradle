apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'http://dl.bintray.com/cyrillrx/maven' }
}

android {
    compileSdkVersion project.ANDROID_BUILD_SDK_VERSION as int
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    // customize apk output names
    // For each application variant (release, debug, etc.)
    applicationVariants.all { variant ->
        def outputs = variant.getOutputs()
        outputs.each { output ->
            File originalFile = output.outputFile
            String date = (new Date()).format('yyyy-MM-dd')
            String newFilename = date + '_' + originalFile.getName()
            output.outputFile = new File(originalFile.parent, newFilename)
        }
    }

    defaultConfig {
        applicationId project.GROUP + ".uremote"
        minSdkVersion project.ANDROID_BUILD_MIN_SDK_VERSION as int
        targetSdkVersion project.ANDROID_BUILD_TARGET_SDK_VERSION as int
        versionCode 1
        versionName "0.7.1"
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('../keystore/debug.keystore')
        }

        release {
            storeFile rootProject.file('../keystore/release.keystore')

            File propertiesFile = rootProject.file('../keystore/keystore.properties')
            Properties properties = new Properties();
            properties.load(new FileInputStream(propertiesFile));

            storePassword properties['storePassword']
            keyAlias properties['keyAlias']
            keyPassword properties['keyPassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"

            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release

            ext.betaDistributionEmailsFilePath = 'beta_distribution_emails.txt'
            ext.betaDistributionReleaseNotesFilePath = 'release_notes.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {

    // Local libs

    // Google Support libraries
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:cardview-v7:23.2.1'
    compile 'com.android.support:recyclerview-v7:23.2.1'

    // Testing
    testCompile 'junit:junit:4.12'

    // External libraries

    compile 'com.cyrillrx.android:notifier:0.1.1'
    compile 'com.cyrillrx.android:core:0.3.1'
    compile 'com.cyrillrx.android:remote:0.0.2'
    compile 'com.cyrillrx.android:logger-extended:1.2.1'
    compile 'com.cyrillrx.android:tracker-extended:0.3.0'
    compile 'com.google.protobuf:protobuf-java:3.0.0-beta-2'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') { transitive = true; }
}
